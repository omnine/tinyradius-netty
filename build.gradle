plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
    id "org.sonarqube" version "4.0.0.2929"
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.globalreachtech'
version = '1.5.13-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'io.netty:netty-codec:4.1.92.Final'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'

    testImplementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation('net.jradius:jradius-core:1.1.5') {
        transitive = false // for reference implementation of hashing utilities
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "globalreachtech_tinyradius-netty"
        property "sonar.organization", "globalreachtech"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}



task testClientJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'org.tinyradius.io.TestClient'
    }
    from('src/test/resources') {
        include 'freeradius/*.*'
    }

    from sourceSets.test.output
    with jar
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourcesJar
                artifact javadocJar
            }
            pom {
                name = project.name
                description = 'TinyRadius-Netty is a small Java Radius library'
                url = 'https://github.com/globalreachtech/tinyradius-netty'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
                    }
                }
                developers {
                    developer {
                        id = "horaceli"
                        url = "https://github.com/horaceli"
                    }
                    developer {
                        id = 'globalreachtech'
                        organizationUrl = 'https://www.globalreachtech.com/'
                    }
                }
                scm {
                    url = 'https://github.com/globalreachtech/tinyradius-netty'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

